#!/usr/bin/env perl

use strict;
use warnings;

use JSON;
use Data::Dumper;
use IO::Socket::INET;
use Getopt::Long;

#flush after ever write
$| = 1;

sub minerCmd {
	my $host = shift;
	my $cmd = shift;
	my $param = shift;

	my $port = 4028;

	my $cmdHash = {
		command => $cmd,
	};
	if(defined $param) {
		$cmdHash->{parameter} = $param;
	}
	
	my $jsonString = encode_json($cmdHash);

	my $sock = new IO::Socket::INET(PeerAddr => $host, PeerPort => $port, Proto => 'tcp');
	if (!$sock) {
		die("Could not connect to host '$host': $!");
	}
	$sock->timeout(5);

	$sock->send($jsonString);

	my $ret = '';
	while ($sock->read(my $in,1024)) {
		$ret .= $in;
	}

	$sock->close();

	my $retHash = decode_json($ret);

	return $retHash;
}

sub checkTemp {
	my $host = shift;
	my $gpu = shift;
	my $warn = shift;
	my $crit = shift;
	
	my $data = minerCmd($host, 'gpu', $gpu);

	my $ret = {
		retVal => 0,
	};

	my $temp = $data->{GPU}->[0]->{Temperature};
	if (!$temp) {
		$ret->{retVal} = 3;
		$ret->{retString} = "Could not get Temperature from server";
		return $ret;
	}

	my $status = 'OK';
	
	if ($temp >= $warn) {
		$ret->{retVal} = 1;
		$status = "Warning";
	}
	if ($temp >= $crit) {
		$ret->{retVal} = 2;
		$status = "Critical";
	}

	$ret->{retString} = "Temperature $status " . $temp . '|' .$temp;

	return $ret;
}

sub checkHash {
	my $host = shift;
	my $gpu = shift;
	my $warn = shift;
	my $crit = shift;


	my $data = minerCmd($host, 'gpu', $gpu);

	my $ret = {
		retVal => 0,
	};

	my $hash = $data->{GPU}->[0]->{'MHS 5s'};
	if (!$hash) {
		$ret->{retVal} = 3;
		$ret->{retString} = "Could not get MHash/s from server";
		return $ret;
	}

	my $status = 'OK';
	
	if ($hash <= $warn) {
		$ret->{retVal} = 1;
		$status = "Warning";
	}
	if ($hash <= $crit) {
		$ret->{retVal} = 2;
		$status = "Critical";
	}

	$ret->{retString} = "MHash/s $status " . $hash . '|' .$hash;

	return $ret;
}

MAIN: {
	my $host;
	my $gpu;
	my $tempWarn;
	my $tempCrit;
	my $hashWarn;
	my $hashCrit;
	GetOptions(
		"host|h=s" => \$host,
		"gpu|g=s" => \$gpu,
		"tempWarn|tw=i" => \$tempWarn,
		"tempCrit|tc=i" => \$tempCrit,
		"hashWarn|hw=f" => \$hashWarn,
		"hashCrit|hc=f" => \$hashCrit,
	);


	my $ret = {
		retVal => 3,
		retString => 'Usage:...'
	};

	if ($tempWarn && $tempCrit) {
		$ret = checkTemp($host, $gpu, $tempWarn, $tempCrit);
	} elsif ($hashCrit && $hashWarn) {
		$ret = checkHash($host, $gpu, $hashWarn, $hashCrit);
	}
	print $ret->{retString} . "\n";
	exit $ret->{retVal};
}
